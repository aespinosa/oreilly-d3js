<!DOCTYPE HTML>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=utf-8" />

  <title>D3 playground</title>
  
  <script src="vendor/js/coffee-script.js" type="text/javascript" charset="utf-8"></script>
  <script src="vendor/js/d3.v2.js" type="text/javascript" charset="utf-8"></script>

  <style type="text/css" media="all">
    .axis path, line {
      stroke: black;
    }
    .line {
      float: left;
    }
    .line_container {
      width: 150px;
      height: 20px;
    }
    path {
      fill: none;
    }
    .key {
      float: right
    }
    .key_line {
      font-size: 17px;
      width: 100%;
    }
    .key_square {
      height: 10px;
      width: 10px;
      outline: solid 1px black;
      float: left;
      margin: 6px 10px 0px 10px;
    }
    #timeseries {
      float: left;
    }

    .timeseries path {
      stroke: black;
      stroke-width: 0.5px;
    }
  </style>
</head>
<body>
  <div id="timeseries">
  </div>

  <div id="key">
  </div>

  <script type="text/coffeescript" charset="utf-8">
    time_scale = null
    percent_scale = null

    draw_timeseries = (data, id) ->
      line = d3.svg.line()
        .x((d) -> time_scale(d.time))
        .y((d) -> percent_scale(d.late_percent))
        .interpolate("linear")

      g = d3.select("#chart")
        .append("g")
          .attr("id", "#{id}_path")
          .attr("class", "timeseries #{id}")

      g.append("path")
        .attr("d", line(data))

      g.selectAll("circle")
        .data(data)
        .enter()
        .append("circle")
          .attr("cx", (d) -> time_scale(d.time))
          .attr("cy", (d) -> percent_scale(d.late_percent))
          .attr("r", 5)

      g.selectAll("circle")
        .on("mouseover", (d) ->
            d3.select(this)
              .transition()
              .attr("r", 9)
        )
        .on("mouseout", (d) ->
            d3.select(this)
              .transition()
              .attr("r", 5)
        )

      g.selectAll("circle")
        .on("mouseover.tooltip", (d) ->
            d3.select("text##{d.line_id}").remove()
            d3.select("#chart")
              .append("text")
              .text("#{d.late_percent} %")
              .attr("x", time_scale(d.time) + 10)
              .attr("y", percent_scale(d.late_percent) - 10)
              .attr("id", d.line_id)
        )
      g.selectAll("circle")
        .on("mouseout.tooltip", (d) ->
            console.log(d)
            d3.select("text##{d.line_id}")
              .transition()
              .duration(500)
              .style("opacity", 0)
              .attr("transform", "translate(10, -10)")
              .remove()
        )


    get_time_series_data = () ->
      id = d3.select(this).attr("id")
      ts = d3.select("##{id}_path")
      
      if ts.empty()
        d3.json("data/subway_wait.json", (data) ->
          filtered_data = data.filter((d) -> return d.line_id == id)
          draw_timeseries(filtered_data, id)
        )
      else
        ts.remove()

    draw = (data) ->
      "use strict"

      container_dimensions = {width: 900, height: 400}
      margins = {top: 10, right: 20, bottom: 30, left: 60}
      chart_dimensions = {
          width: container_dimensions.width - margins.left - margins.right,
          height: container_dimensions.height - margins.top - margins.bottom
      }

      chart = d3.select("#timeseries")
        .append("svg")
          .attr("width", container_dimensions.width)
          .attr("height", container_dimensions.height)
        .append("g")
          .attr("transform", "translate(#{margins.left}, #{margins.top})")
          .attr("id", "chart")

      time_scale = d3.time.scale()
        .range([0, chart_dimensions.width])
        .domain([new Date(2008, 10, 1), new Date(2011, 3, 1)])

      percent_scale = d3.scale.linear()
        .range([chart_dimensions.height, 0])
        .domain([65, 90])

      time_axis = d3.svg.axis()
        .scale(time_scale)
      count_axis = d3.svg.axis()
        .scale(percent_scale)
        .orient("left")

      chart.append("g")
        .attr("class", "x axis")
        .attr("transform", "translate(0, #{chart_dimensions.height})")
        .call(time_axis)
      chart.append("g")
        .attr("class", "y axis")
        .call(count_axis)
      d3.select(".y.axis")
        .append("text")
          .attr("text-anchor", "middle")
          .text("percent on time")
          .attr("transform", "rotate (-270, 0, 0)")
          .attr("x", container_dimensions.height/2)
          .attr("y", 50)

      key_items = d3.select("#key")
        .selectAll("div")
        .data(data)
        .enter()
        .append("div")
          .attr("class", "key_line")
          .attr("id", (d) -> return d.line_id)

      key_items.append("div")
        .attr("id", (d) -> return "key_square_#{d.line_id}")
        .attr("class", "key_square")

      key_items.append("div")
        .attr("class", "key_label")
        .text((d) -> return d.line_name)

      d3.selectAll(".key_line")
        .on("click", get_time_series_data)

    d3.json "data/subway_wait_mean.json", draw
  </script>
</body>
</html>
